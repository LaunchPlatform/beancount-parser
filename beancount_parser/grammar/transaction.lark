TXN: "txn"
NARRATION: ESCAPED_STRING
PAYEE_NARRATION: ESCAPED_STRING _WS+ ESCAPED_STRING
_NL_INDENT: _NL _WS+

annotation_item: TAG | LINK
annotations: annotation_item+

transaction_header: DATE [TXN | FLAG] [PAYEE_NARRATION | NARRATION] [annotations] [COMMENT]
transaction_body: (_NL_INDENT (posting | metadata_line))+
transaction: transaction_header transaction_body [_NL]

%import common.ESCAPED_STRING
%import common.NEWLINE -> _NL
%import common.WS_INLINE -> _WS

%import .comment.COMMENT
%import .flag.FLAG
%import .numbers.NUMBER
%import .numbers.SIGNED_NUMBER
%import .account.ACCOUNT
%import .currency.CURRENCY
%import .tag.TAG
%import .link.LINK
%import .date.DATE
%import .posting.posting
%import .metadata.metadata_line
%import .metadata.metadata_lines
