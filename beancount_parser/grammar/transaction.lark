// Directives
INCOMPLETE: "!"
COMPLETED: "*"
FLAG: INCOMPLETE | COMPLETED

// Transaction
TXN: "txn"
PAYEE: ESCAPED_STRING
NARRATION: ESCAPED_STRING
LETTER_DIGIT: LETTER | DIGIT
CURRENCY: LETTER_DIGIT+

amount: NUMBER CURRENCY
signed_amount: SIGNED_NUMBER CURRENCY
price: amount

per_unit_cost: "{" amount "}"
total_cost: "{{" amount "}}"
both_cost: "{" NUMBER "#" amount "}"
dated_cost: "{" amount "," date "}"
cost: per_unit_cost | total_cost | both_cost | dated_cost

posting: [FLAG] ACCOUNT signed_amount [cost] ["@" price] [COMMENT]
transaction_header: date [TXN | FLAG] [[PAYEE] NARRATION] [COMMENT]
transaction: transaction_header (NEWLINE posting)+

%import common.DIGIT
%import common.ESCAPED_STRING
%import common.LETTER
%import common.NEWLINE

%import .comment.COMMENT
%import .date.date
%import .numbers.NUMBER
%import .numbers.SIGNED_NUMBER
%import .account.ACCOUNT
