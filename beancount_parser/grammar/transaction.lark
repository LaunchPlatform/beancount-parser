// Directives
INCOMPLETE: "!"
COMPLETED: "*"
FLAG: INCOMPLETE | COMPLETED

// Transaction
TXN: "txn"
PAYEE: ESCAPED_STRING
NARRATION: ESCAPED_STRING
LETTER_DIGIT: LETTER | DIGIT
CURRENCY: LETTER_DIGIT+

amount: NUMBER CURRENCY
signed_amount: SIGNED_NUMBER CURRENCY

per_unit_price: "@" amount
total_price: "@@" amount
price: per_unit_price | total_price

per_unit_cost: "{" amount "}"
total_cost: "{{" amount "}}"
both_cost: "{" NUMBER "#" amount "}"
dated_cost: "{" amount "," date "}"
cost: per_unit_cost | total_cost | both_cost | dated_cost

long_posting: [FLAG] ACCOUNT signed_amount [cost] [price] [COMMENT]
// the special case where only Account is present
short_posting: [FLAG] ACCOUNT [COMMENT]
posting: long_posting | short_posting

transaction_header: date [TXN | FLAG] [[PAYEE] NARRATION] [COMMENT]
transaction_body: (_NL _WS_INLINE+ posting)+ [_NL]
transaction: transaction_header transaction_body

%import common.DIGIT
%import common.ESCAPED_STRING
%import common.LETTER
%import common.NEWLINE -> _NL
%import common.WS_INLINE -> _WS_INLINE

%import .comment.COMMENT
%import .date.date
%import .numbers.NUMBER
%import .numbers.SIGNED_NUMBER
%import .account.ACCOUNT
