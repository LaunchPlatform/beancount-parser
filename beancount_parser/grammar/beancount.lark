_EMPTY_LINE: _WS* _NL+
COMMENT_LINE: _WS* COMMENT [_NL]
_NL_INDENT: _NL _WS+

_directives_with_metadata: open
            | close
            | event
            | note
            | document
            | price
            | balance
            | pad
            | commodity
_directives_without_metadata: include
            | option
            | plugin
_directive: (_directives_with_metadata [(_NL_INDENT metadata_line)+] | _directives_without_metadata) [_NL]

start: (_directive | _EMPTY_LINE | COMMENT_LINE)*

%import common.WS_INLINE -> _WS
%import common.NEWLINE -> _NL

%import .comment.COMMENT
%import .open.open
%import .close.close
%import .transaction.transaction
%import .event.event
%import .note.note
%import .document.document
%import .price.price
%import .balance.balance
%import .pad.pad
%import .commodity.commodity
%import .include.include
%import .option.option
%import .plugin.plugin
%import .metadata.metadata_line

%ignore _WS
