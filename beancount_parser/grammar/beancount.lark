currencies: CURRENCY ("," CURRENCY)*
amount: SIGNED_NUMBER CURRENCY

// Metadata
METADATA_KEY: _WS+ LCASE_LETTER (LETTER | DIGIT | "-" | "_")*
?metadata_value: ESCAPED_STRING
                    | ACCOUNT
                    | CURRENCY
                    | DATE
                    | SIGNED_NUMBER
                    | TAGS
                    | amount
metadata_item: METADATA_KEY ":" metadata_value

// Date directives
?open:      DATE "open" ACCOUNT [currencies] [ESCAPED_STRING]
?close:     DATE "close" ACCOUNT
?balance:   DATE "balance" ACCOUNT amount
?event:     DATE "event" ESCAPED_STRING ESCAPED_STRING
?commodity: DATE "commodity" CURRENCY
?document:  DATE "document" ACCOUNT ESCAPED_STRING
?note:      DATE "note" ACCOUNT ESCAPED_STRING
?pad:       DATE "pad" ACCOUNT amount
?price:     DATE "price" CURRENCY amount

?all_date_directive: open
                | close
                | balance
                | event
                | commodity
                | document
                | note
                | pad
date_directive: all_date_directive (CNL metadata_item)*

// Simple directives
?option: "option" ESCAPED_STRING ESCAPED_STRING
?include: "include" ESCAPED_STRING
?plugin: "plugin" ESCAPED_STRING [ESCAPED_STRING]

?simple_directive: option
                 | include
                 | plugin

// Posting
?per_unit_cost: "{" amount "}"
?total_cost: "{{" amount "}}"
?both_cost: "{" SIGNED_NUMBER  "#" amount "}"
?dated_cost: "{" amount "," DATE "}"
cost: per_unit_cost | total_cost | both_cost | dated_cost

?per_unit_price: "@" amount
?total_price: "@@" amount
tx_price: per_unit_price | total_price

?detailed_posting: [FLAG] ACCOUNT amount [cost] [tx_price]
// the special case where only Account is present
?simple_posting: [FLAG] ACCOUNT
?all_posting: detailed_posting | simple_posting
posting: all_posting (CNL metadata_item)*

// Transaction
NARRATION: ESCAPED_STRING
PAYEE: ESCAPED_STRING

annotation_item: TAG | LINK
annotations: annotation_item+

?txn_header: DATE ["txn" | FLAG] [[PAYEE] NARRATION] [annotations]
txn_header_meta: txn_header (CNL metadata_item)*
transaction_body: posting+
transaction: txn_header_meta transaction_body

directive: date_directive | simple_directive | transaction
statement: directive [CNL]
start: statement*

%import common.ESCAPED_STRING
%import common.WS_INLINE -> _WS
%import common.NEWLINE -> _NL
%import common.LCASE_LETTER
%import common.LETTER
%import common.DIGIT

%import .comment.COMMON_OR_NEWLINE -> CNL
%import .numbers.SIGNED_NUMBER
%import .account.ACCOUNT
%import .flag.FLAG
%import .currency.CURRENCY
%import .date.DATE
%import .tag.TAGS
%import .tag.TAG
%import .link.LINK

%ignore _WS
%ignore CNL
