currencies: CURRENCY ("," CURRENCY)*
amount: SIGNED_NUMBER CURRENCY

// Metadata
METADATA_KEY: LCASE_LETTER (LETTER | DIGIT | "-" | "_")*
?metadata_value: ESCAPED_STRING
                    | ACCOUNT
                    | CURRENCY
                    | DATE
                    | SIGNED_NUMBER
                    | TAGS
                    | amount
metadata_item: METADATA_KEY ":" metadata_value

// Date directives
annotation_item: TAG | LINK
annotations: annotation_item+

?open:      DATE "open" ACCOUNT [currencies] [ESCAPED_STRING]
?close:     DATE "close" ACCOUNT
?balance:   DATE "balance" ACCOUNT amount
?event:     DATE "event" ESCAPED_STRING ESCAPED_STRING
?commodity: DATE "commodity" CURRENCY
?document:  DATE "document" ACCOUNT ESCAPED_STRING
?note:      DATE "note" ACCOUNT ESCAPED_STRING
?pad:       DATE "pad" ACCOUNT ACCOUNT
?price:     DATE "price" CURRENCY amount
?txn:       DATE ["txn" | FLAG] [[ESCAPED_STRING] ESCAPED_STRING] [annotations]

date_directive: open
                | close
                | balance
                | event
                | commodity
                | document
                | note
                | pad
                | price
                | txn

// Simple directives
?option: "option" ESCAPED_STRING ESCAPED_STRING
?include: "include" ESCAPED_STRING
?plugin: "plugin" ESCAPED_STRING [ESCAPED_STRING]

simple_directive: option
                 | include
                 | plugin

// Posting
?per_unit_cost: "{" amount "}"
?total_cost: "{{" amount "}}"
?both_cost: "{" SIGNED_NUMBER  "#" amount "}"
?dated_cost: "{" amount "," DATE "}"
cost: per_unit_cost | total_cost | both_cost | dated_cost

?per_unit_price: "@" amount
?total_price: "@@" amount
tx_price: per_unit_price | total_price

?detailed_posting: [FLAG] ACCOUNT amount [cost] [tx_price]
// the special case where only Account is present
?simple_posting: [FLAG] ACCOUNT
posting: detailed_posting | simple_posting

?directive: date_directive
        | simple_directive
        | posting
        | metadata_item
statement: directive [COMMENT] | COMMENT | ORG_ANCHOR
?start: (statement _NL)* [statement]

%import common.ESCAPED_STRING
%import common.WS_INLINE -> _WS
%import common.NEWLINE -> _NL
%import common.LCASE_LETTER
%import common.LETTER
%import common.DIGIT

%import .comment.COMMENT
%import .org_anchor.ORG_ANCHOR
%import .numbers.SIGNED_NUMBER
%import .account.ACCOUNT
%import .flag.FLAG
%import .currency.CURRENCY
%import .date.DATE
%import .tag.TAGS
%import .tag.TAG
%import .link.LINK

%ignore _WS
