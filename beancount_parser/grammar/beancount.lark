// Date
FOUR_DIGIT: DIGIT~4
TWO_DIGIT: DIGIT~2
YEAR: FOUR_DIGIT
MONTH: TWO_DIGIT
DAY: TWO_DIGIT
date: YEAR "-" MONTH "-" DAY

// Directives
INCOMPLETE: "!"
COMPLETED: "*"
FLAG: INCOMPLETE | COMPLETED

SIGNED_DECIMAL: ["-" | "+"] DECIMAL

// Transaction
TXN: "txn"
PAYEE: ESCAPED_STRING
NARRATION: ESCAPED_STRING
LETTER_DIGIT: LETTER | DIGIT
ACCOUNT: (LETTER_DIGIT+) [":" (LETTER_DIGIT+)]*
CURRENCY: LETTER_DIGIT+

amount: DECIMAL CURRENCY
signed_amount: SIGNED_DECIMAL CURRENCY
price: amount

per_unit_cost: "{" amount "}"
total_cost: "{" amount "}"
both_cost: "{" DECIMAL "#" amount "}"
dated_cost: "{" amount "," date "}"
cost: per_unit_cost | total_cost | both_cost | dated_cost

posting: [FLAG] ACCOUNT signed_amount [cost] ["@" price]

transaction: date [TXN | FLAG] [[PAYEE] NARRATION] (NEWLINE posting)+

start: transaction

%import common.DIGIT
%import common.ESCAPED_STRING
%import common.LETTER
%import common.DECIMAL
%import common.NEWLINE
%ignore " "
