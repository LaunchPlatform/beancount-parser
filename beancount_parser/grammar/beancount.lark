currencies: CURRENCY ("," CURRENCY)*
amount: SIGNED_NUMBER CURRENCY

?open:      DATE "open" ACCOUNT [currencies] [ESCAPED_STRING]
?close:     DATE "close" ACCOUNT
?balance:   DATE "balance" ACCOUNT amount
?event:     DATE "event" ESCAPED_STRING ESCAPED_STRING
?commodity: DATE "commodity" CURRENCY
?document:  DATE "document" ACCOUNT ESCAPED_STRING
?note:      DATE "note" ACCOUNT ESCAPED_STRING

directive: open
        | close
        | balance
        | event
        | commodity
        | document
        | note

statement: directive

start: directive

%import common.ESCAPED_STRING
%import common.WS_INLINE -> _WS

%import .numbers.SIGNED_NUMBER
%import .comment.COMMENT
%import .comment.NEWLINE
%import .account.ACCOUNT
%import .currency.CURRENCY
%import .date.DATE

%ignore _WS
%ignore NEWLINE
